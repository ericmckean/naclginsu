#!/usr/bin/python
#
# Copyright (c) 2010 The Ginsu Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

""" Main scons script for Ginsu builds.
"""

import os
import sys

if os.getenv('NACL_SDK_BASE') is None:
  sys.stderr.write('NACL_SDK_BASE must be defined as the root directory of NaCl'
                   'toolchain.\n')
  sys.exit(1)
if not os.path.exists(os.path.join(os.getenv('NACL_SDK_BASE'), 'nacl', 'usr',
                                             'include', 'boost')):
  sys.stderr.write('Your NaCl toolchain does not have the necessary packages\n'
                   'Run scripts/setup_naclports_packages.py\n')
  sys.exit(1)

environment_list = []

base_env = Environment(
  tools = ['component_setup'],
  BUILD_SCONSCRIPTS = [
    '$MAIN_DIR/third_party/cgal/cgal.scons',
    '$MAIN_DIR/third_party/glu_tessellator/glu_tessellator.scons',
    '$MAIN_DIR/c_salt/c_salt.scons',
    '$MAIN_DIR/ginsu/ginsu.scons',
    '$MAIN_DIR/model/model.scons',
    '$MAIN_DIR/view/view.scons',
  ],
  CPPPATH = ['$MAIN_DIR'],
  CPPDEFINES = [
    'BOOST_ALL_NO_LIB',
    'CGAL_NO_AUTOLINK_CGAL'
  ],
)

#-----------------------------------------------------------------------------
# Build environment for NaCl module
nacl_env = base_env.Clone(
    tools = ['naclsdk'],
    BUILD_GROUPS = ['default'],
    BUILD_TYPE_DESCRIPTION = 'NaCl module',
    BUILD_TYPE = 'nacl',
    LIBS = ['google_nacl_imc',
            'google_nacl_npruntime',
            'pthread',
            'srpc'],
    NATIVE_CLIENT_SDK_DEFAULT_MODE='custom:' + os.getenv('NACL_SDK_BASE')
)

nacl_dbg_env = nacl_env.Clone(
        BUILD_GROUPS = ['default'],
        BUILD_TYPE = 'dbg',
        tools = ['target_debug']
)
environment_list.append(nacl_dbg_env)

nacl_opt_env = nacl_env.Clone(
        BUILD_GROUPS = ['default'],
        BUILD_TYPE = 'opt',
        tools = ['target_optimized']
)
environment_list.append(nacl_opt_env)

BuildComponents(environment_list)
