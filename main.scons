#!/usr/bin/python
#
# Copyright (c) 2010 The Ginsu Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

""" Main scons script for Ginsu builds.
"""

environment_list = []

base_env = Environment(
  tools = ['component_setup'],
  BUILD_SCONSCRIPTS = [
    '$MAIN_DIR/third_party/boost/boost.scons',
    '$MAIN_DIR/third_party/cgal/cgal.scons',
    '$MAIN_DIR/third_party/chromium/chromium.scons',
    '$MAIN_DIR/third_party/glu_tessellator/glu_tessellator.scons',
    '$MAIN_DIR/third_party/openscenegraph/openscenegraph.scons',
    '$MAIN_DIR/ginsu/ginsu.scons',
    '$MAIN_DIR/model/model.scons',
    '$MAIN_DIR/view/view.scons',
  ],
  CPPPATH = ['$MAIN_DIR'],
  CPPDEFINES = [
    'BOOST_ALL_NO_LIB',
    'CGAL_NO_AUTOLINK_CGAL',
    'CGAL_NDEBUG',
  ],
)

# -----------------------------------------------------------------------------
# Linux environment
linux_env = base_env.Clone(
  tools = ['target_platform_linux'],
  CCFLAGS = [
    '-fPIC',
    '-frounding-math',
    '-fvisibility=hidden',
    '-fvisibility-inlines-hidden',
  ],
)

linux_dbg_env = linux_env.Clone(
  BUILD_GROUPS = ['default'],
  BUILD_TYPE = 'dbg',
  tools = ['target_debug'],
)
environment_list.append(linux_dbg_env)

# -----------------------------------------------------------------------------
# OS X environment
mac_env = base_env.Clone(
  tools = ['target_platform_mac'],
)

mac_dbg_env = mac_env.Clone(
  BUILD_GROUPS = ['default'],
  BUILD_TYPE = 'dbg',
  tools = ['target_debug'],
)
environment_list.append(mac_dbg_env)

# -----------------------------------------------------------------------------
# Windows environment
windows_env = base_env.Clone(
  tools = ['target_platform_windows'],
)
windows_env.Append(
  CPPDEFINES = [
    '_CRT_SECURE_NO_DEPRECATE',
    '_SCL_SECURE_NO_WARNINGS',
    'NOMINMAX',
    '_UNICODE',
    'UNICODE',
    '_WINDOWS',
    'WIN32',
    'WIN32_LEAN_AND_MEAN',
  ],
  ENABLE_EXCEPTIONS = True,
)

windows_dbg_env = windows_env.Clone(
  BUILD_GROUPS = ['default'],
  BUILD_TYPE = 'dbg',
  tools = ['target_debug'],
)
environment_list.append(windows_dbg_env)

windows_opt_env = windows_env.Clone(
  BUILD_TYPE = 'opt',
  tools = ['target_optimized'],
)
environment_list.append(windows_opt_env)

# -----------------------------------------------------------------------------
# Process the build environments
BuildEnvironments(environment_list)
