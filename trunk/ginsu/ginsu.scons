#!/usr/bin/python
#
# Copyright (c) 2010 The Ginsu Project Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

Import('env')

env.Append(
  CPPPATH = [
    '$MAIN_DIR/third_party/boost/trunk',
    '$MAIN_DIR/third_party/chromium/src',
    '$MAIN_DIR/third_party/chromium/src/gpu',
    '$MAIN_DIR/third_party/npapi/bindings',
  ],
)

ginsu_sources = [
  'ginsu.cc',
  'plugin.cc',
]

ginsu_input_libs = [
  'ginsu_view',
  'ginsu_model',
  'osgUtil',
  'osg',
  'OpenThreads',
  'pgl',
  'gles2',
  'CGAL',
  'boost_thread',
  'glu_tessellator',
]

if env.Bit('windows'):
  ginsu_sources += ['plugin.def']
  ginsu_sources += env.RES('plugin.rc')
  
  env.ComponentLibrary(
    'ginsu',
    ginsu_sources,
    LIBS = ginsu_input_libs,
    COMPONENT_STATIC=False,
    COMPONENT_LIBRARY_PUBLISH=True,
    COMPONENT_LIBRARY_LINK_SUFFIXES=[],
  )

if env.Bit('mac'):
  mac_plugin_env = env.Clone(
        LINKFLAGS = ['-bundle', '-framework', 'Foundation'])
  # Build the ginsu binary
  binary = mac_plugin_env.ComponentProgram('ginsu',
                                           ginsu_sources,
                                           LIBS = ginsu_input_libs,
                                           COMPONENT_STATIC=False,
                                           COMPONENT_LIBRARY_PUBLISH=True,
                                           COMPONENT_LIBRARY_LINK_SUFFIXES=[])
  # Create resource fork
  RESOURCE_DIR = env.SubstList2('${MAIN_DIR}')[0] + '/ginsu/'
  RESOURCE_DESC = RESOURCE_DIR + 'ginsu.r' 
  RESOURCE_FORK = 'ginsu.rsrc'
  REZ = '/Developer/Tools/Rez'
  env.Command(target = RESOURCE_FORK,
              source = RESOURCE_DESC,
              action = [Action(REZ + ' -o ${TARGET} ${SOURCE} -useDF')])
  # Create bundle plist
  PLIST_FILE = 'Info.plist'
  ACTION = ("cp -f ${SOURCE} ${TARGET};"
            "chmod +w ${TARGET};"
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleExecutable ginsu' ${TARGET};"
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleIdentifier com.google.ginsu' ${TARGET};"
            "/usr/libexec/PlistBuddy -c 'Set :CFBundleName ginsu' ${TARGET}")

  env.Command(target = PLIST_FILE,
              source = RESOURCE_DIR + 'ginsu-info.plist',
              action = [Action(ACTION)])


  plugin_name = '${STAGING_DIR}/' + 'ginsu.plugin'
  mac_plugin_env.Bundle(plugin_name,
                        BUNDLE_EXE = binary[0],
                        BUNDLE_INFO_PLIST = [PLIST_FILE],
                        BUNDLE_PKGINFO_FILENAME = 0,
                        BUNDLE_RESOURCES = RESOURCE_FORK)
